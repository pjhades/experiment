NB. https://en.wikipedia.org/wiki/Levi-Civita_symbol
NB. It's the parity of permutation used when computing determinants
NB.
NB. `lc` is the function to generate the matrices whose entries
NB. are all possible Levi-Civita values, in any given dimension
NB.
NB. In 2D, the matrix consists of
NB. [ ε11 ε12 ]   [  0 1 ]
NB. [ ε21 ε22 ] = [ -1 0 ]

lc=: C.!.2 @ (#:i.) @ $~

NB. Given argument n, $~ first computes n$n, determining the shape
NB. so if n=5 then the matrix shape will be 5 5 5 5 5, very full
NB.
NB. Then we apply fork (#: i.) on top, in case of n=3 we have
NB. 3 3 3 #: i. 3 3 3
NB.
NB. i. 3 3 3 generates a shape 3 3 3 matrix with values from 0 to (n^n)-1
NB.
NB. Then for each value, we convert it to base 3 3 3, which is basically
NB. the digits under base 3, so that values from the previous step
NB. 0 1 2 will become 0 0 0, 0 0 1, 0 0 2. Also because #: generates
NB. an array for each value so the output shape will become 3 3 3 3.
NB.
NB. Finally we apply permutation shape C.!.2 on top, computing the
NB. Levi-Civita symbol of each array from last step. Note that that step
NB. gives us a bunch of arrays consisting of 0 1 and 2, some of which
NB. can be regarded as a permutation while others can not.
NB. This step each array like 0 1 2 will be converted to a parity sign
NB. thus the output shape will go back to 3 3 3.

NB. With the help of this matrix we can compute generalized cross product.
NB. Recall that cross product can be computed as determinant:
NB.
NB.         |  i  j  k |   (u2v3 - u3v2) i +
NB. u x v = | u1 u2 u3 | = (u3v1 - u1v3) j +
NB.         | v1 v2 v3 |   (u1v2 - u2v1) k
NB.
NB. By arranging the output sign matrices we can construct a matrix
NB. multiplication:
NB.
NB.            [   0 -v3  v2 ]
NB. (u1 u2 u3) [  v3   0 -v1 ]
NB.            [ -v2  v1   0 ]
NB.
NB. Apparently this can be done by multiplying each 3x3 matrix
NB. generated by (lc 3) and the right vector of the cross product:
NB.
NB. |: (lc 3) (+/ . *) y
NB.
NB. So in general we can do it with

mp=: +/ . *
cross=: [ mp [: |: lc@#@[ mp ]
